# Copyright (c) 2017 Marco Iorio (giorio94 at gmail dot com)
# This file is part of Share Your Files (SYF) and has been partially
# generated by QtCreator.

# SYF is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# SYF is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with SYF.  If not, see <http://www.gnu.org/licenses/>.

QT       += core gui network qml quickcontrols2
greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TARGET = ShareYourFiles
VERSION = 1.0
TEMPLATE = app

DEFINES += TARGET=\\\"$$TARGET\\\"
DEFINES += VERSION=\\\"$$VERSION\\\"

# Check Qt version
QT_MAJOR_VERSION_REQ = 5
QT_MINOR_VERSION_REQ = 8
QT_VERSION_REQ = $${QT_MAJOR_VERSION_REQ}.$${QT_MINOR_VERSION_REQ}

ERROR_STR = $${TARGET} requires Qt $${QT_VERSION_REQ} or newer but Qt $${QT_VERSION} was detected.

# Check major version
lessThan(QT_MAJOR_VERSION, $${QT_MAJOR_VERSION_REQ}) : \
    error($${ERROR_STR})

# Check minor version
equals(QT_MAJOR_VERSION, $${QT_MAJOR_VERSION_REQ}) : \
    lessThan(QT_MINOR_VERSION, $${QT_MINOR_VERSION_REQ}) : \
        error($${ERROR_STR})
# End check

# The following define makes your compiler emit warnings if you use
# any feature of Qt which as been marked as deprecated (the exact warnings
# depend on your compiler). Please consult the documentation of the
# deprecated API in order to know how to port your code away from it.
DEFINES += QT_DEPRECATED_WARNINGS

# You can also make your code fail to compile if you use deprecated APIs.
# In order to do so, uncomment the following line.
# You can also select to disable deprecated APIs only up to a certain version of Qt.
DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x060000    # disables all the APIs deprecated before Qt 6.0.0

# Enable C++11 support
CONFIG += C++11

# Add some more warnings.
QMAKE_CXXFLAGS += -Wall -Wextra -Wpedantic

SOURCES += main.cpp \
    shareyourfiles.cpp \
    Common/common.cpp \
    Common/networkentrieslist.cpp \
    Common/threadpool.cpp \
    UserDiscovery/syfddatagram.cpp \
    UserDiscovery/syfdprotocol.cpp \
    UserDiscovery/user.cpp \
    UserDiscovery/users.cpp \
    UserDiscovery/usericon.cpp \
    UserDiscovery/syfitprotocol.cpp \
    FileTransfer/syfpprotocol.cpp \
    FileTransfer/syfftprotocolserver.cpp \
    FileTransfer/syfftprotocolcommon.cpp \
    FileTransfer/syfftprotocolreceiver.cpp \
    FileTransfer/syfftprotocolsender.cpp \
    FileTransfer/fileinfo.cpp \
    FileTransfer/fileintransfer.cpp \
    FileTransfer/transferinfo.cpp \
    FileTransfer/transferlist.cpp \
    Gui/Wrappers/peersselectormodel.cpp \
    Gui/Wrappers/settingsmodel.cpp \
    Gui/Wrappers/transferrequestmodel.cpp \
    Gui/Wrappers/transfersmodel.cpp \
    Gui/Wrappers/transferresponsemodel.cpp \
    Gui/Wrappers/duplicatedfilemodel.cpp

HEADERS  += \
    shareyourfiles.hpp \
    Common/common.hpp \
    Common/networkentrieslist.hpp \
    Common/threadpool.hpp \
    UserDiscovery/syfddatagram.hpp \
    UserDiscovery/syfdprotocol.hpp \
    UserDiscovery/user.hpp \
    UserDiscovery/users.hpp \
    UserDiscovery/userinfo.hpp \
    UserDiscovery/usericon.hpp \
    UserDiscovery/receptionpreferences.hpp \
    UserDiscovery/syfitprotocol.hpp \
    FileTransfer/syfpprotocol.hpp \
    FileTransfer/syfftprotocolserver.hpp \
    FileTransfer/syfftprotocolcommon.hpp \
    FileTransfer/syfftprotocolreceiver.hpp \
    FileTransfer/syfftprotocolsender.hpp \
    FileTransfer/fileinfo.hpp \
    FileTransfer/fileintransfer.hpp \
    FileTransfer/transferinfo.hpp \
    FileTransfer/transferlist.hpp \
    Gui/Wrappers/peersselectormodel.hpp \
    Gui/Wrappers/settingsmodel.hpp \
    Gui/Wrappers/transferrequestmodel.hpp \
    Gui/Wrappers/transfersmodel.hpp \
    Gui/Wrappers/transferresponsemodel.hpp \
    Gui/Wrappers/duplicatedfilemodel.hpp

RESOURCES += \
    resources.qrc

# Properties (Windows)
RC_ICONS = ../icon.ico
QMAKE_TARGET_PRODUCT = "Share Your Files"
QMAKE_TARGET_DESCRIPTION = "Peer to peer - on LAN - file sharing"
QMAKE_TARGET_COPYRIGHT = "Copyright 2017 Marco Iorio - GNU GPL3 license"

# CuteLogger library
win32:CONFIG(release, debug|release): LIBS += -L$$OUT_PWD/../CuteLogger/release/ -lCuteLogger
else:win32:CONFIG(debug, debug|release): LIBS += -L$$OUT_PWD/../CuteLogger/debug/ -lCuteLogger
else:unix: LIBS += -L$$OUT_PWD/../CuteLogger/ -lCuteLogger

INCLUDEPATH += $$PWD/../CuteLogger/include
DEPENDPATH += $$PWD/../CuteLogger/include

# Create target docs to generate the documentation.
unix {
    # Check if doxygen is available
    DOXYGEN_BIN = $$system(which doxygen)
    isEmpty(DOXYGEN_BIN) {
        message("Doxygen not found - documentation building will be skipped")
    }

    # If available generate docs target
    else {
        docs.commands = DOXYGEN_INPUT_DIR=$$PWD \
                        DOXYGEN_OUTPUT_DIR=$$OUT_PWD/Documentation \
                        doxygen $$PWD/Doxyfile
        QMAKE_EXTRA_TARGETS += docs
        QMAKE_POST_LINK += $$docs.commands

        QMAKE_DISTCLEAN += -r $$DOCS_OUTPUT
    }
}

win32 {
    message("Automatic documentation build is not yet supported under windows!")
}
